import com.github.jengelman.gradle.plugins.shadow.transformers.*

plugins {
    id 'scala'
    id 'com.github.johnrengelman.shadow' version '7.0.0'
    id 'org.springframework.boot' version '2.6.3'
}

group 'r.ian'
version '1.0-SNAPSHOT'

repositories {
    mavenCentral()
}

configurations {
    provided
    compile.extendsFrom provided
}

dependencies {
    implementation 'org.scala-lang:scala-library:2.12.12'
    implementation ('org.apache.spark:spark-sql_2.12:3.3.0') {
        //on spark class path
        exclude group: 'com.google.code.gson', module: 'gson'
    }
    implementation (group: 'org.springframework.boot', name: 'spring-boot-starter', version: '2.6.3') {
        //spark has slf4-log4j impl on classpath
        exclude group: 'ch.qos.logback', module: 'logback-classic'
        exclude group: 'org.apache.logging.log4j', module: 'log4j-to-slf4j'
        exclude group: 'org.slf4j', module: 'jul-to-slf4j'
//        exclude group: 'org.yaml', module: 'snakeyaml'
    }
    testImplementation 'org.scalatest:scalatest_2.12:3.1.1'
    testImplementation 'org.scalatestplus:junit-4-12_2.13:3.1.2.0'
}

test {
    useJUnitPlatform()
}

shadowJar {
    zip64 true
    //for correct META-INF
    mergeServiceFiles()
    append 'META-INF/spring.handlers'
    append 'META-INF/spring.schemas'
    append 'META-INF/spring.tooling'
    transform(PropertiesFileTransformer) {
        paths = ['META-INF/spring.factories' ]
        mergeStrategy = "append"
    }
}
